ext.componentDescription = "Adoration Support Tool"

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

mainClassName="website.magyar.adoration.engine.AdorationApplication"

def configFileDefaultPath = "$project.projectDir/adoration.conf.properties";
if (!hasProperty("configFilePath")) {
    project.ext.configFilePath = configFileDefaultPath
}

run {
    args = ["$configFilePath"];
}

dependencies {
    compile project(':adoration-application:adoration-database')
    compile project(':adoration-application:adoration-core')
    compile project(':adoration-application:adoration-webapp')
}

tasks.named('jar') {
    manifest {
        attributes('Main-Class': 'website.magyar.adoration.engine.AdorationApplication')
        attributes('Implementation-Title': "$componentDescription - ${rootProject.version}")
        attributes('Version': "${rootProject.version}")
    }
}

shadowJar {
        append('META-INF/spring.handlers')
        append('META-INF/spring.schemas')

        excludes = [
            'META-INF/*.SF',
            'META-INF/*.DSA',
            'META-INF/*.RSA',
            'images/',
            '*.txt',
            '*.gif',
            '*.png',
            '*.dtd',
            '*.xcf',
            'build.properties',
            'about.html']
}

jar.finalizedBy shadowJar
